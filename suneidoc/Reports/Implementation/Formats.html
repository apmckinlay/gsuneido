<h3>Formats</h3>

<p>Formats are constructed using _report.Construct()
</p>

<p>Formats use the following methods:
</p>

<pre>GetSize(data = ?) => Object(w:, h:, d:)
</pre>

<p>Returns the width, height, and descent of the format.
</p>

<pre>Print(x, y, w, h, data = ?)</pre>

<p>Prints the format at position x,y with the specified width and height.  If the format is stretchable, the width and height may be larger than what GetSize returned.  
</p>

<pre>Variable?( ) => true or false (default false)</pre>

<p>Returns true if the format's size depends on the data (e.g. Wrap).
</p>

<pre>Generator?( ) => true or false</pre>

<p>Returns true if the format is a generator.
</p>

<p>and the following public members (with their default values):
</p>

<pre>
X = 0
Y = 0
Xstretch = 0
Ystretch = 0
Data
Field
</pre>

<p>Report.Construct automatically copies certain members from the format specification to the constructed format instance:
</p>

<pre>
x
y
xstretch
ystretch
field
</pre>

<p>Formats have access to their containing report via:
</p>

<pre>_report</pre>

<p>For example:
</p>

<pre>
_report.GetDC( ) // get the current device context

_report.Construct( ) // construct a format

_report.SelectFont( )

_report.GetFont( )
</pre>

<p>If a format changes the current font (using _report.SelectFont) 
it is also responsible for changing it back:
</p>

<pre>
oldfont = _report.SelectFont( .font );
...
_report.SelectFont( oldfont );
</pre>

<p>Formats can be very simple, for example here is Vfill:
</p>

<pre>
Format
    {
    Ystretch: 1
    }
</pre>

<p>Vskip is a little more complicated because it allows you to override the height:
</p>

<pre>
Format
    {
    Size: #( h: 240, w: 0, d: 0 );
    New( height = false )
        {
        if ( height isnt false )
            .Size = Object( h: height * 1440, w: 0, d: 0 );
        }
    GetSize( data = false )
        {
        return .Size;
        }
    }
</pre>

<p>The general outline for an actual printing format should be:
</p>

<pre>
Format
    {
    New(data = false, ...)
        {
        .Data = data;
        ...
        }
    GetSize(data = "")
        {
        if (.Data isnt false)
            data = .Data;
        ...
        }
    Print(x, y, w, h, data = "")
        {
        if (.Data isnt false)
            data = .Data;
        ...
        }
    }
</pre>

<p>Notice how the data may be supplied either when the format is created, or when it is used.  
This allows the Flyweight pattern to be used, so the formats can be reused.
</p>

<p>For example here is a simple text format:</p>

<pre>
DrawTextFormat
Format
    {
    New(data = false, font = false)
        {
        .Data = data;
        .font = font;
        }
    GetSize(data = "")
        {
        if (.Data isnt false)
            data = .Data;
        data $= "";
        oldfont = _report.SelectFont(.font);
        dc = _report.GetDC();
        flags = DT.SINGLELINE +
            DT.CALCRECT + DT.EXTERNALLEADING;
        DrawTextEx(dc, data, -1, r = Object(), flags, 0);
        _report.SelectFont(oldfont);
        return Object(h: r.bottom, w: r.right, d: tm.Descent);
        }
    Print(x, y, w, h, data = "")
        {
        if (.Data isnt false)
            data = .Data;
        data $= "";
        oldfont = _report.SelectFont(.font);
        flags = DT.SINGLELINE;
        rect = Object(left: x, right: x + w, top: y, bottom: y + h);
        DrawTextEx(_report.GetDC(), data, -1, rect, flags, 0);
        _report.SelectFont(oldfont);
        }
    }
</pre>

<p>This could be used like:</p>

<pre>( DrawText "hello world" font: ( name: "Arial", size: 20 ) )</pre>