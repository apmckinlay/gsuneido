<h2>Creating a Form Report</h2>

<p>A common type of form report is an invoice with a header and then line items below.  This section will describe how to write a report for a simple invoice form.</p>

<p>First we need to create the tables, go to QueryView and run the following (each "create" statement must be run separately):</p>

<pre>
create invoice (customer, invoice, invoice_number) key(invoice)

create invoicelines (invoice, line_num, price, product, qty)
    index(invoice) in invoice cascade
    key(line_num)
</pre>

<p>Remember when entering test data into these tables, invoice must be unique in the invoice table, and the values for invoice in the invoicelines table must exist in the invoice table in the invoice column (foreign key). The <a href="Master-Detail Relationship.html">Master-Detail Relationship</a> section shows how to maintain the foreign key automatically.</p>

<p>In the following definitions, the first line is the name of the definition (type this in the tree view on the left side in Library View). These definitions should be placed in mylib or some library other than stdlib.  If you have any problems with the following definitions, you can load in the supplied mylib and use it, it contains all of the following definitions.</p>

<p>We can set up a rule to calculate the total price on each line item:</p>

<pre>
<b>Rule_total_price</b>
function()
   {
   return Number(.qty) * Number(.price)
   }</pre>

<p>Now we can define the report for the invoice in Library View:</p>

<pre>
<b>My_Invoice_Rpt</b>
#(Params
    title: "Invoice"
    name: "Invoice_Rpt"
    Params:
        (Form
            (ParamsSelect invoice_number)
        )
    QueryFormat
        {
        Query()
            {
            where = _report.GetParamsWhere("invoice_number")
            return "invoice " $ where $ " sort invoice"
            }
        Before_invoice(data)
            {
            return Object("Vert"
                Object("Text", "Invoice " $ data.invoice_number,
                    font: #(name: 'Arial' size: 12 weight: 600))
                "customer")
            }
        Output: false
        After_invoice(data)
            {
            .Append(Object(QueryFormat
                {
                Query()
                    {
                    return  "invoicelines where invoice is " $ Display(.Hdr.invoice);
                    }
                Output: (Row product qty price total_price)
                }
                Hdr: data))
            return 'pg'
            }
        }
    )</pre>

<p>In the report definition above, a QueryFormat is used.</p>
 
<p>The Query method returns the query by using _report.GetParamsWhere to build the where clause for the restriction on the invoice field.  In order to use _report.GetParamsWhere, the ParamsSelectControl must be used for the fields passed to GetParamsWhere.  Notice the use of ParamsSelectControl for the invoice parameter in the Params section of the report.</p>

<p>Before_invoice is a method that gets called before every invoice is printed.  It returns a format specification to print.  We can define these methods Before_invoice and After_invoice because we have sorted on this field.  In our Before_invoice we return a format for the header portion of the report. This is simply a Vert container with a format for each item we want to print on the header.  We specified the format for invoice, but notice that just the field name was used for customer.  This means the format will be retrieved from the datadict entry for the field (This would be defined as Field_customer in the library).</p>

<p>Next we have the actual Output. In this case we don't want to do anything in Output so we set this member to false.  Output could be defined as a method that takes data, which is the output record, and returns a format to print.</p>

<p>In After_invoice, we use the Append method to add a format to the end of the report, this time it is another QueryFormat for the line items.  Notice how the data gets passed to the Query method of the QueryFormat.  This time the Output is defined as a RowFormat.  Since we used .Append to add the line items, we can still return a format from After_invoice.  We will return "pg" so that each invoice starts on a new page.</p>

<p>To go farther with this invoice example, including setting up invoice entry screens, see the <a href="Master-Detail Relationship.html">Master-Detail Relationship</a> section.</p>