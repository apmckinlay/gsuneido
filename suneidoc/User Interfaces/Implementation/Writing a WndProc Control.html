<h3>Writing a WndProc Control</h3>

<p><a href="../Reference/WndProc.html">WndProc</a> controls "wrap" a Windows control for use in Suneido user interfaces. WndProc inherits from Hwnd, adding the ability to respond to WM window messages.</p>

<p>For example, here is a simplified version of the EtchedLine WndProc control:</p>

<pre>
<b>MyEtchedLineControl</b>

WndProc
    {
    Ymin: 6
    Xstretch: 1
    New()
        {
        .CreateWindow("SuneidoWindow", "", WS.VISIBLE)
        .SubClass()
        .shadowpen = CreatePen(PS.SOLID, 0,
            GetSysColor(COLOR.BTNSHADOW))
        .highlightpen = CreatePen(PS.SOLID, 0,
            GetSysColor(COLOR.BTNHIGHLIGHT))
        }
    PAINT()
        {
        hdc = BeginPaint(.Hwnd, ps = Object())
        GetClientRect(.Hwnd, r = Object())
        oldPen = SelectObject(hdc, .shadowpen)
        MoveTo(hdc, r.left, (r.top + r.bottom) / 2 - .5)
        LineTo(hdc, r.right, (r.top + r.bottom) / 2 - .5)
        SelectObject(hdc, .highlightpen)
        MoveTo(hdc, r.left, (r.top + r.bottom) / 2 + .5)
        LineTo(hdc, r.right, (r.top + r.bottom) / 2 + .5)
        SelectObject(hdc, oldPen)
        EndPaint(.Hwnd, ps)
        return 0
        }
    DESTROY()
        {
        DeleteObject(.shadowpen)
        DeleteObject(.highlightpen)
        return 0
        }
    }
</pre>

<p>Note:</p>
<ul>
<li>inherit from WndProc</li>

<li>create the Win32 control</li>

<li>SubClass in order to handle WM_ windows messages</li>

<li>handle the necessary window messages e.g. define PAINT to handle WM_PAINT</li>

<li>release any resources in DESTROY</li>
</ul>

<p>If you don't need to handle any WM window messages, use <a href="../Reference/Hwnd.html">Hwnd</a> instead.</p>

<p>See also:
<a href="Writing a Hwnd Control.html">Writing a Hwnd Control</a>
</p>