<h2>Introduction</h2>

<p>User interfaces in Suneido are constructed from <i>controls</i>. Controls are responsible for drawing themselves so windows do not have a "paint" method and they are responsible for responding to input from the user. This is similar to the way Windows dialogs work. However, the layout of controls is done using <a href="../Appendix/Patterns/Boxes and Stretch.html">Boxes and Stretch</a> rather than using a dialog editor.</p>

<p>Suneido controls include standard Windows controls such as buttons and combo boxes, 
common controls such as ListView and TreeView, as well as custom Suneido controls.  
New controls can easily be written, either based on existing Windows controls, or from scratch.</p>

<p>Unlike Windows controls, Suneido controls are fully <i>encapsulated</i>. Windows notifications which would normally be sent to parent windows are reflected back to the controls. All output from controls is done by sending messages to controllers. This encapsulation makes it easier to combine controls since they do not interfere with each other. Keeping controllers separate from the visual controls also simplifies the system and allows more reuse.</p>

<p>Here is a very simple Window with a single Static control that displays "Hello world!"</p>

<pre>Window(#(Static, "Hello world!"))</pre>

<p>Multiple controls can be included using Vert and Horz:</p>

<pre>
Window(#(Vert
    (Static "Hello world!")
    (Horz (Button "OK") (Button "Cancel"))
    ))
</pre>

<p>More complicated user interfaces use <i>Controllers </i>to handle interaction with the user.</p>

<pre>
Window(Controller
    {
    Title: "Simple Editor"
    Commands:
        (
        (Cut,       "Ctrl+X",   "Cut the selected text to the clipboard")
        (Copy,      "Ctrl+C",   "Copy the selected text to the clipboard")
        (Paste,     "Ctrl+V",   "Insert the contents of the clipboard")
        )
    Menu:
        (
        ("&Edit",
            "Cu&t", "&Copy", "&Paste")
        )
    Controls:
        (Vert
            (Toolbar, Cut, Copy, Paste)
            Editor)
    })
</pre>


<p>Note:</p>
<ul>
<li>text to go in the window's title bar is defined with Title:</li>

<li>commands must be defined, with their accelerator keys and status bar explanations, before being used on menus or toolbars</li>

<li>the editor automatically stretches to fill the window, initially as well as when the window is resized</li>
</ul>

<p>Here is a layout (non-functional) for a debugger window:</p>

<pre>
#(VertSplit
    (Tabs
        (Explorer (LibTreeModel) (Scintilla) Tab: Source)
        (Editor Tab: Output)
        ystretch: 3)
    (HorzSplit
        (ListBox,
            "x       123",
            "n       456",
            "list    (1, 2, 3)")
        (ListBox, "one", "two", "three", "four")
        )
    )
</pre>